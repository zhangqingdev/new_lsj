apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "com.oushangfeng.lsj"
        minSdkVersion 16
        targetSdkVersion 19
        versionCode 100
        versionName "1.0.0"
    }

    signingConfigs {
        demo {
            storeFile file("../sign.jks")
            storePassword "123456789"
            keyAlias "lsj"
            keyPassword "123456789"
            v2SigningEnabled false;
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.demo
            debuggable true
        }
        debug {
//            applicationIdSuffix ".debug"
            signingConfig signingConfigs.demo
        }
    }

    //修改生成的apk名字
    applicationVariants.all { variant ->

        if (variant.buildType.name.equals('release') || variant.buildType.name.equals('debug')) {
            variant.outputs.each { output ->
                def appName = 'lsj'
                def oldFile = output.outputFile
                def buildName = ''

                variant.productFlavors.each { product ->
                    buildName = product.name
                }

                def releaseApkName = appName + '-' + defaultConfig.versionName + "-" + variant.buildType.name + "-" + buildName + '.apk'
                output.outputFile = new File(oldFile.parent, releaseApkName)
            }
        }
    }

    // 解决butterknife和lombok的冲突：Duplicate files copied in APK META-INF/services/javax.annotation.processing.Processor
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/java-gen']
        }
    }



}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:support-annotations:25.0.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.9'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    // 使用okhttp3加载图片
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'de.greenrobot:greendao:2.1.0'
    compile 'zhou.widget:richtext:1.0.4'
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.github.zhaokaiqiang.klog:library:1.5.0'
    compile('com.afollestad.material-dialogs:core:0.9.0.1') {
        transitive = true
    }
    //compile 'com.squareup.retrofit2:converter-gson:2.0.2'

    compile project(':changeskin')
    compile project(':slideback')

    compile 'com.makeramen:roundedimageview:2.3.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
}